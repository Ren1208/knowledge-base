#бизнес_аналитика #реализация #факторный_анализ  

---
## Постановка задачи и исходные данные

Рассмотрим, как можно сравнить продажи предприятия за выбранный и предыдущий год используя модуль [[Применение и способы визуализации факторного анализа|факторного анализа]], разработанный в Microsoft Power BI. Это даст возможность увидеть, как менялись объёмы продаж со временем. Фактором, по которому будет проводиться анализ, является название товара.

В качестве [[Исходные данные для факторного анализа||исходных данных]] была взята информация о продажах за 4 года некоторой торговой фирмы, содержащая даты оказания услуг, их наименования и стоимость. 

На выходе хотим получить дашборд, который показывал бы следующее:

-   Объём продаж за выбранный и предыдущий год.
-   Рост (или падение) продаж в виде процентного отношения между данными за два сравниваемых года.
-   Изменение продаж для каждого товара в виде водопадной диаграммы.
-   Наличие интерактивного выбора года, позволяющего мгновенно обновлять данные в соответствии с выбранным годом.

Модули будут написаны на языке DAX (Data Analysis Expressions). Это язык формул, используемый в Power BI и других технологиях Microsoft, разработанный для работы с данными в моделях памяти и создания новых вычислений и анализа данных.

В процессе работы на языке DAX будут реализованы меры. Мера — это формула для вычисления агрегированных значений. Меры динамичны и пересчитываются при изменении данных или фильтров в отчете. Для построения дашборда будут реализованы меры для подсчета объема и <strong style="color: green;">роста</strong> <strong style="color: red;">(или падения)</strong> продаж предприятия.

Для интерактивного выбора года будет настроен срез. Срез — это визуальный элемент управления, позволяющий фильтровать данные на дашборде по определенному критерию. Срезы могут быть представлены в виде списков, ползунков или других интерфейсов, и позволяют интерактивно изменять отображаемые данные.

---
## Реализация

Начнём с реализации функции интерактивного выбора года. Для этого потребуется отдельная таблица для среза года, которая не будет фильтровать «Календарь» (таблицу дат). Такую таблицу можно создать с помощью функции VALUES, которая используется для возвращения уникального набора значений из столбца.

```sql
СрезГод = VALUES('Календарь'[Year])
```

Далее, добавляем созданную таблицу в срез. Интерактивный выбор года готов.

Следующим шагом, чтобы в водопадной диаграмме показать изменение продаж для каждого товара за выбранный и предыдущий год, потребуется две меры.

```sql
KPI = SUM('Данные'[сумма])
```

Выше определяется мера, подсчитывающая KPI (ключевой показатель эффективности), который является суммой значений в столбце «сумма» таблицы «Данные». Теперь необходимо использовать результат этой меры для расчёта изменений KPI между выбранным и предыдущим годом.

```sql
KPI waterfall =
CALCULATE(
     [KPI],
     FILTER(
          'Календарь',
          [Year] = MAX('СрезГод'[Year]) ||
          [Year] = MAX('СрезГод'[Year]) – 1
     )
)
```

Эта мера рассчитывает KPI (ключевой показатель эффективности) для выбранного и предыдущего года, который будет использован для создания водопадной диаграммы, показывающей изменения KPI между этими двумя годами. 

Функция CALCULATE изменяет контекст фильтрации для вычисления KPI. Это означает, что она будет вычислять KPI на основе фильтров, которые задаёт функция FILTER. Эта функция применяется к таблице «Календарь» и создает таблицу, которая содержит только те строки, которые соответствуют условию фильтрации. В данном случае, условие фильтрации — это строки, в которых год равен максимальному году в таблице «СрезГод» (выбранный год) или на один год меньше (предыдущий год).

> [!warning] 
> Заметим, что за 2021 год есть не все данные, а только по сентябрь. Для его сравнения с 2020 годом данные должны быть сопоставимыми – 2020 год тоже должен быть только по сентябрь. 

Для ограничения отображения периодов добавим вычисляемый столбец в таблице «Календарь» как показано в следующей мере:

```sql
DatesCY-PY =
var _maxDataDate = MAX('Данные'[дата])
var _maxYear = YEAR(maxDataDate)
var _prevYear = YEAR(maxDataDate) -1

var _maxCompareDate =
IF ('Календарь'[Year] = _prevYear,
'Календарь'[Date] <= EOMONTH(maxDataDate, -12),
'Календарь'[Date] <= _maxDataDate)
return _maxCompareDate
```

Функция EOMONTH возвращает последний день месяца, который находится на указанное количество месяцев до или после начальной даты. В данном контексте, если текущий год в таблице «Календарь» соответствует предыдущему году (переменной prevYear), то функция EOMONTH помогает сравнить даты только до конца того же месяца в предыдущем году, обеспечивая корректное сопоставление периодов.

Теперь необходимо внести корректировки в меру KPI waterfall.

```sql
KPI waterfall =
CALCULATE(
	[KPI],
	FILTER('Календарь','Календарь'[Year] = MAX('СрезГод'[Year]))
) +
CALCULATE (
	[KPI],
	FILTER (
		'Календарь',
		'Календарь'[Year] = MAX('СрезГод'[Year])-1 &&
		'Календарь'[DatesCY-PY] = TRUE()
    )
)
```

Почти готово. Осталось добавить на дашборд заголовок, объём и рост (или падение) продаж за выбранный и предыдущий год. Заголовок реализован следующим образом:

```sql
заголовок = "Факторный анализ, "
& MAX('СрезГод'[Year]) & " и " & MAX('СрезГод'[Year]) -1 &
IF (
	MAX('СрезГод'[Year]) = YEAR(MAX('Данные'[дата])),
	" (сопоставимый) год",
	" год"
)
```

Реализация объёма продаж за выбранный и предыдущий год представлены в следующих двух мерах:

```sql
kpi CY =
CALCULATE (
	[KPI],
	FILTER (
		'Календарь',
		'Календарь'[Year] = MAX( 'СрезГод'[Year]))
)
```

```sql
kpi PY сопост. =
CALCULATE (
	[KPI],
	FILTER (
		'Календарь',
		'Календарь'[Year] = MAX('СрезГод'[Year])-1 &&
		'Календарь'[DatesCY-PY] = TRUE()
	)
)
```

Для вычисления роста (или падения) продаж в виде процентного отношения определена следующая мера:

```sql
рост % = DIVIDE([kpi CY] - [kpi PY сопост.], [kpi PY сопост.])
```

Функция DIVIDE вычитает KPI предыдущего года из KPI текущего года, чтобы найти абсолютное изменение, а затем делит это изменение на KPI предыдущего года, чтобы получить процентное изменение.

После выполнения всех необходимых действий была автоматически создана модель данных, которая позволяет наглядно представить связи между таблицами и мерами.

![[Реализация факторного анализа.png]]

Последним этапом размещаем созданные меры на дашборд и получаем необходимые [[Дашборды модуля факторного анализа|визуализации]].